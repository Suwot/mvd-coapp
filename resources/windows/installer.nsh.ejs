ManifestDPIAware true

######################################################################

# Explicit non-elevated execution - CRITICAL for user-only installation
RequestExecutionLevel user

######################################################################

!define APP_NAME "MAX Video Downloader CoApp"
!define COMP_NAME "MAX Video Downloader"
!define VERSION "0.5.2"
!define COPYRIGHT "Rostislav"
!define DESCRIPTION "MAX Video Downloader CoApp"
<% if (target.arch == "x86_64") { %>
!define INSTALLER_NAME "mvdcoapp-0.5.2-win-x64.exe"
<% } else { %>
!define INSTALLER_NAME "mvdcoapp-0.5.2-win-arm64.exe"
<% } %>
!define MAIN_APP_EXE "mvdcoapp.exe"
!define ICON "icon.ico"
!define LICENSE_TXT "../LICENSE.txt"
!define INSTALL_DIR "$LOCALAPPDATA\${APP_NAME}"
!define INSTALL_TYPE "SetShellVarContext current"
!define REG_ROOT "HKCU"
!define REG_APP_PATH "Software\Microsoft\Windows\CurrentVersion\App Paths\${MAIN_APP_EXE}"
!define UNINSTALL_PATH "Software\Microsoft\Windows\CurrentVersion\Uninstall\${APP_NAME}"

######################################################################

VIProductVersion  "${VERSION}"
VIAddVersionKey "ProductName"  "${APP_NAME}"
VIAddVersionKey "CompanyName"  "${COMP_NAME}"
VIAddVersionKey "LegalCopyright"  "${COPYRIGHT}"
VIAddVersionKey "FileDescription"  "${DESCRIPTION}"
VIAddVersionKey "FileVersion" "${VERSION}"

######################################################################

SetCompressor /SOLID Lzma
Name "${APP_NAME}"
Caption "${APP_NAME}"
OutFile "${INSTALLER_NAME}"
BrandingText "${APP_NAME}"
InstallDirRegKey "${REG_ROOT}" "${REG_APP_PATH}" ""
InstallDir "${INSTALL_DIR}"

######################################################################

!define MUI_ICON "${ICON}"
!define MUI_UNICON "${ICON}"

######################################################################

!include "MUI2.nsh"

!define MUI_ABORTWARNING
!define MUI_UNABORTWARNING

!insertmacro MUI_PAGE_WELCOME

!ifdef LICENSE_TXT
!insertmacro MUI_PAGE_LICENSE "${LICENSE_TXT}"
!endif

!insertmacro MUI_PAGE_DIRECTORY

!insertmacro MUI_PAGE_INSTFILES

!insertmacro MUI_PAGE_FINISH

!insertmacro MUI_UNPAGE_CONFIRM

!insertmacro MUI_UNPAGE_INSTFILES

!insertmacro MUI_UNPAGE_FINISH

!insertmacro MUI_LANGUAGE "English"

######################################################################

Section -MainProgram
	${INSTALL_TYPE}

  SetDetailsPrint textonly
  DetailPrint "Installing CoApp..."
  SetDetailsPrint listonly

	SetOverwrite ifnewer
	SetOutPath "$INSTDIR"
	File /r "install_dir\\"

SectionEnd

######################################################################

Section -Icons_Reg
SetOutPath "$INSTDIR"
WriteUninstaller "$INSTDIR\uninstall.exe"

WriteRegStr ${REG_ROOT} "${REG_APP_PATH}" "" "$INSTDIR\${MAIN_APP_EXE}"
WriteRegStr ${REG_ROOT} "${UNINSTALL_PATH}"  "DisplayName" "${APP_NAME}"
WriteRegStr ${REG_ROOT} "${UNINSTALL_PATH}"  "UninstallString" "$INSTDIR\uninstall.exe"
WriteRegStr ${REG_ROOT} "${UNINSTALL_PATH}"  "DisplayIcon" "$INSTDIR\${MAIN_APP_EXE}"
WriteRegStr ${REG_ROOT} "${UNINSTALL_PATH}"  "DisplayVersion" "${VERSION}"
WriteRegStr ${REG_ROOT} "${UNINSTALL_PATH}"  "Publisher" "${COMP_NAME}"

# Register native messaging host for Chrome/Chromium browsers
WriteRegStr ${REG_ROOT} "SOFTWARE\Google\Chrome\NativeMessagingHosts\bkblnddclhmmgjlmbofhakhhbklkcofd" "" "$INSTDIR\bkblnddclhmmgjlmbofhakhhbklkcofd.json"
WriteRegStr ${REG_ROOT} "SOFTWARE\Microsoft\Edge\NativeMessagingHosts\bkblnddclhmmgjlmbofhakhhbklkcofd" "" "$INSTDIR\bkblnddclhmmgjlmbofhakhhbklkcofd.json"
WriteRegStr ${REG_ROOT} "SOFTWARE\BraveSoftware\Brave\NativeMessagingHosts\bkblnddclhmmgjlmbofhakhhbklkcofd" "" "$INSTDIR\bkblnddclhmmgjlmbofhakhhbklkcofd.json"
WriteRegStr ${REG_ROOT} "SOFTWARE\Vivaldi\NativeMessagingHosts\bkblnddclhmmgjlmbofhakhhbklkcofd" "" "$INSTDIR\bkblnddclhmmgjlmbofhakhhbklkcofd.json"
WriteRegStr ${REG_ROOT} "SOFTWARE\Opera Software\NativeMessagingHosts\bkblnddclhmmgjlmbofhakhhbklkcofd" "" "$INSTDIR\bkblnddclhmmgjlmbofhakhhbklkcofd.json"

# Register native messaging host for Firefox
WriteRegStr ${REG_ROOT} "SOFTWARE\Mozilla\NativeMessagingHosts\max-video-downloader@rostislav.dev" "" "$INSTDIR\max-video-downloader@rostislav.dev.json"

SectionEnd

######################################################################

Section Uninstall
${INSTALL_TYPE}

RmDir /r "$INSTDIR"

DeleteRegKey ${REG_ROOT} "${REG_APP_PATH}"
DeleteRegKey ${REG_ROOT} "${UNINSTALL_PATH}"

# Remove native messaging host registrations
DeleteRegKey ${REG_ROOT} "SOFTWARE\Google\Chrome\NativeMessagingHosts\bkblnddclhmmgjlmbofhakhhbklkcofd"
DeleteRegKey ${REG_ROOT} "SOFTWARE\Microsoft\Edge\NativeMessagingHosts\bkblnddclhmmgjlmbofhakhhbklkcofd"
DeleteRegKey ${REG_ROOT} "SOFTWARE\BraveSoftware\Brave\NativeMessagingHosts\bkblnddclhmmgjlmbofhakhhbklkcofd"
DeleteRegKey ${REG_ROOT} "SOFTWARE\Vivaldi\NativeMessagingHosts\bkblnddclhmmgjlmbofhakhhbklkcofd"
DeleteRegKey ${REG_ROOT} "SOFTWARE\Opera Software\NativeMessagingHosts\bkblnddclhmmgjlmbofhakhhbklkcofd"
DeleteRegKey ${REG_ROOT} "SOFTWARE\Mozilla\NativeMessagingHosts\max-video-downloader@rostislav.dev"

SectionEnd